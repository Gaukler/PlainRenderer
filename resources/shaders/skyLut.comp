#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

#include "global.inc"
#include "sky.inc"
#include "lightBuffer.inc"
#include "volumeShading.inc"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set=1, binding = 0, r11f_g11f_b10f) uniform image2D skyLut;
layout(set=1, binding = 1) uniform texture2D transmissionLut;
layout(set=1, binding = 2) uniform texture2D multiscatterLut;

layout(set=1, binding = 4, std140) uniform atmosphereSettingsBuffer{
    AtmosphereSettings atmosphereSettings;
};

layout(set=1, binding = 5, std430) buffer lightStorageBuffer{
    LightBuffer lightBuffer;
}; 

float shadowRay(vec3 P, vec3 D, vec3 C, float earthRadius){
    vec3 L = C - P;
    float t_ca = dot(L, D);
    float d = sqrt(dot(L, L) - t_ca * t_ca);
    
    //earth intersection
    float t_hc_earth = sqrt(earthRadius * earthRadius - d * d);
    float t_earth = t_ca - t_hc_earth; 
    
    return t_earth > 0 ? 0 : 1; 
}

void main(){
	ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    
    float x = uv.x / float(imageSize(skyLut).x);
    float y = uv.y / float(imageSize(skyLut).y);
    
    vec3 V = fromSkyLut(vec2(x, y)); //from camera to sky
    
    vec3 earthCenter = vec3(0); 
    float bias = 0.002f;    //need to offset to avoid self intersection with earth
    vec3 P = vec3(0, -atmosphereSettings.earthRadius - bias, 0);
    Intersection intersection = rayEarthIntersection(P, V, earthCenter, atmosphereSettings.earthRadius, atmosphereSettings.atmosphereHeight);
      
    const int sampleCount = 30;
    float stepSize = intersection.distance / sampleCount; 

    vec3 L = g_sunDirection.xyz;
    
    float VoL = dot(V, L);
    float phaseRayleigh = phaseRayleigh(VoL);

    //float phaseMie = phaseGreenstein(VoL, atmosphereSettings.mieScatteringExponent);
    float phaseMie = cornetteShanksPhase(VoL, atmosphereSettings.mieScatteringExponent);
    //float phaseMie = phaseSchlick(-VoL, atmosphereSettings.mieScatteringExponent);

    vec3 currentPosition = P;
    vec3 absorption = vec3(1.f);
    vec3 color = vec3(0);
    vec3 step = V * stepSize;
    
    for(int i = 0; i < sampleCount; i++){
        
        currentPosition += step;
        
        vec3 up = currentPosition - earthCenter;
        float upLength = length(up);
        float currentHeight = upLength - atmosphereSettings.earthRadius;
        up /= upLength;
         
        //light arriving at point
        vec2 lutUV = computeLutUV(currentHeight, atmosphereSettings.atmosphereHeight, up, L);
        vec3 transmission = texture(sampler2D(transmissionLut, g_sampler_linearClamp), lutUV).rgb;
        vec3 incomingLight = lightBuffer.sunStrengthExposed * transmission;
        incomingLight *= shadowRay(currentPosition, L, earthCenter, atmosphereSettings.earthRadius);
                
        AtmosphereCoefficients coefficients = calculateCoefficients(currentHeight, atmosphereSettings);
        
        //inscattering from light at point
        vec3 inscatteringRayleight = coefficients.scatterRayleigh * incomingLight * phaseRayleigh;
        vec3 inscatteringMie = coefficients.scatterMie * incomingLight * phaseMie;
        vec3 inscattering = inscatteringRayleight + inscatteringMie;
        
        vec3 scatterIntegral = integrateInscattering(inscattering, coefficients.extinction, stepSize);
        color = color + scatterIntegral * absorption;
         
        absorption *= exp(-coefficients.extinction * stepSize); 
        
        vec3 multiscattering = texture(sampler2D(multiscatterLut, g_sampler_linearClamp), lutUV).rgb;
        color += multiscattering * incomingLight * (coefficients.scatterRayleigh + coefficients.scatterMie) * stepSize * transmission;
    }   
    imageStore(skyLut, uv, vec4(color, 0.f));
}