#version 460
#extension GL_ARB_separate_shader_objects : enable
#extension GL_GOOGLE_include_directive : enable

#include "global.inc"
#include "colorConversion.inc"

layout(constant_id = 0) const bool useClipping = false;
layout(constant_id = 1) const bool useVarianceClipping = false;
layout(constant_id = 2) const bool useYCoCg = false;
layout(constant_id = 3) const bool useMotionVectorDilation = false;
layout(constant_id = 4) const bool useDynamicBlendFactor = false;
layout(constant_id = 5) const bool useTonemapping = false;

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set=1, binding = 0, r11f_g11f_b10f) 	uniform image2D framebuffer;
layout(set=1, binding = 1) 	                uniform texture2D previousFramebuffer;
layout(set=1, binding = 2) 	                uniform texture2D motionBuffer;
layout(set=1, binding = 3) 	                uniform texture2D depthBuffer;
layout(set=1, binding = 4)                  uniform sampler colorSampler;

//from: "Temporal ReprojectionAnti-Aliasing in INSIDE"
//clips a point towards a target in a axis aligned bounding box
vec3 clipAABB(
    vec3 target,    //last frame color
    vec3 bbMin,     //bounding box min 
    vec3 bbMax      //bounding box max   
    )    
{   
    const vec3 epsilon = vec3(0.0001f);
    
    vec3 center = 0.5f * (bbMax + bbMin);
    vec3 extend = 0.5f * (bbMax - bbMin) + epsilon; //epsilon prevents divison by zero
    
    vec3 toTarget = target - center;
    vec3 toTargetNorm = toTarget / extend;
    vec3 toTargetNormAbs = abs(toTargetNorm);
    float maxComponent = max(toTargetNormAbs.x, max(toTargetNormAbs.y, toTargetNormAbs.z));

    if(maxComponent < 1.f){
        return target; //target in bounding box
    }
    else{
        return center + toTarget / maxComponent;
    }
}

float luminance(vec3 c){
    return dot(c, vec3(0.21f, 0.71f, 0.08f));
}

vec3 tonemap(vec3 color){
    float luma = luminance(color);
    return color / (luma + 1);
}

vec3 tonemapReverse(vec3 color){
    float luma = luminance(color);
    return color / (1 - luma);
}

vec3 sampleNeighbourhood(ivec2 uv){
    vec3 c = imageLoad(framebuffer, uv).rgb;
    if(useTonemapping){
        c = tonemap(c);
    }
    if(useYCoCg){
        c = linearToYCoCg(c);
    }
    else{
        c = c;
    }
    return c;
}

void main(){

	ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
     
    vec3 currentColor = imageLoad(framebuffer, uv).rgb;
    if(useTonemapping){
        currentColor = tonemap(currentColor);
    }
    
    //collect neighbourhood min/max to build AABB color box
    vec3 minC = currentColor;
    vec3 maxC = currentColor;
    
    if(useYCoCg){
        minC = linearToYCoCg(minC);
        maxC = linearToYCoCg(maxC);
    }
    
    
    vec3 mean = vec3(0.f);
    vec3 variance = vec3(0.f);
    
    for(int x = -1; x <= 1; x++){
        for(int y = -1; y <= 1; y++){
            vec3 c = sampleNeighbourhood(uv + ivec2(x, y));
            mean += c;
            
            minC = min(minC, c);
            maxC = max(maxC, c);
        }
    }
    mean *= 1.f / 9.f;
    
    for(int x = -1; x <= 1; x++){
        for(int y = -1; y <= 1; y++){
            vec3 c = sampleNeighbourhood(uv + ivec2(x, y));
            vec3 diff = mean - c;
            variance += diff * diff;
        }
    }
    variance *= 1.f / 9;
    variance = sqrt(variance);
    
    
    vec2 motion;
    if(useMotionVectorDilation){
        float closestDepth = 1;
        ivec2 closestDepthOffset = ivec2(0);
        
        //3x3 neighbourhood from Inside paper
        for(int x = -1; x <= 1; x++){
            for(int y = -1; y <= 1; y++){
                ivec2 offset = ivec2(x, y);
                float depth = texelFetch(sampler2D(depthBuffer, colorSampler), uv + offset, 0).r;
                if(depth < closestDepth){
                    closestDepth = depth;
                    closestDepthOffset = offset;
                }
            }
        }
        
        motion = texelFetch(sampler2D(motionBuffer, colorSampler), uv + closestDepthOffset, 0).rg;
    }
    else{
        motion = texelFetch(sampler2D(motionBuffer, colorSampler), uv, 0).rg;
    }
    
    //last frame must be sampled linearly and motion vector requires sub-pixel precision
    //careful not to destroy motion vector precision by casting to ivec
    vec2 uvReprojected = (uv + vec2(0.5f)) / vec2(imageSize(framebuffer)) + motion;
    vec3 historySample = texture(sampler2D(previousFramebuffer, colorSampler), uvReprojected).rgb;
    
    if(useTonemapping){
        historySample = tonemap(historySample);
    }
    
    if(useYCoCg){
        historySample = linearToYCoCg(historySample);
    }
    
    vec3 historyUnprocessed = historySample;
    
    if(useVarianceClipping){
        float scale = 1.f;
        
        vec3 varianceMin = mean - variance * scale;
        vec3 varianceMax = mean + variance * scale;
        
        minC = max(varianceMin, minC);
        maxC = min(varianceMax, maxC);
    }

    if(useClipping){
        historySample = clipAABB(historySample, minC, maxC);
    }
    else{
        historySample = clamp(historySample, minC, maxC);
    }
    
    if(useYCoCg){
        historySample = YCoCgToLinear(historySample);
    }
    
    //nan test, mainly a problem when resizing the buffers
    //clear previous frame buffer on resize instead of checking here
    if(isnan(historySample.x) || isnan(historySample.y) || isnan(historySample.z)){
        historySample = currentColor;
    }

    float blendFactorMin = 0.01f;
    float blendFactorMax = 0.1f;

    float lumaHistory = luminance(historyUnprocessed);
    float lumaMin = luminance(minC);
    float lumaMax = luminance(maxC);
    
    float distanceToClamp = min(abs(lumaMin - lumaHistory), abs(lumaMax - lumaHistory));
    distanceToClamp = clamp(distanceToClamp * 1, 0, 1);
    float blendFactor = mix(blendFactorMin, blendFactorMax, distanceToClamp);
    
    vec2 motionInPixels = motion * imageSize(framebuffer);
    float speed = length(motionInPixels);
    speed = clamp(speed, 0, 1);
    blendFactor += mix(0.f, 0.05f, speed);
    
    if(!useDynamicBlendFactor){
        blendFactor = 0.1;
    }
    
    vec3 color = mix(historySample, currentColor, blendFactor);
    
    if(useTonemapping){
        color = tonemapReverse(color);
    }
    
	imageStore(framebuffer, uv, vec4(color, 1.f));
}